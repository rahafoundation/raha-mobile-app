// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath 'com.google.gms:google-services:4.0.1'
        classpath 'io.fabric.tools:gradle:1.25.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
    }
    configurations.all {
        // Firebase libraries still resolve to older versions of Google Play
        // Services, so force it down to 15.0.1.
        // Also, react-native-camera wants an old version of Play Services for vision.
        resolutionStrategy.force "com.google.android.gms:play-services-base:15.0.1"
        resolutionStrategy.force "com.google.android.gms:play-services-basement:15.0.1"
        resolutionStrategy.force "com.google.android.gms:play-services-tasks:15.0.1"
    }

    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            println "Disable pre dexing for module ${project.name}"
            project.android.dexOptions.preDexLibraries = false
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            println "Disable pre dexing for module ${project.name}"
            project.android.dexOptions.preDexLibraries = false
        }
    }
}

ext {
    buildToolsVersion = "26.0.3"
    minSdkVersion = 16
    compileSdkVersion = 26
    targetSdkVersion = 26
    supportLibVersion = "26.1.0"
}
